plugins {
  id 'java'
  id "com.github.johnrengelman.shadow" version "7.1.2"
}

jar {
  manifest {
    attributes 'Main-Class': 'info.batey.djvm.ExampleApplication'
  }
}

shadowJar {
  mergeServiceFiles()
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/*.RSA'
}

sourceCompatibility = 1.8

task copyJar(type: Exec, dependsOn: 'shadowJar') {
  commandLine 'cp', './build/libs/dropwizard-app-all.jar', './docker/'
}

task dockerBuild(type: Exec, dependsOn: 'copyJar') {
  workingDir './docker'
  commandLine 'docker', 'build', '-t', 'dropwizard-app', '.'
}

build.dependsOn(dockerBuild)

project.ext {
  dropwizardVersion = '0.9.2'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
  implementation group: 'io.dropwizard', name: 'dropwizard-client', version: dropwizardVersion
  implementation 'org.glassfish.jersey.ext.rx:jersey-rx-client-java8:2.23'
  implementation 'org.asynchttpclient:async-http-client:2.0.4'
  implementation 'org.asynchttpclient:async-http-client-extras-guava:2.0.4'
  implementation 'com.google.guava:guava:19.0'
  implementation project(":standalone")
}


